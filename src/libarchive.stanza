defpackage libarchive :
  import core
  import collections

;==============================================
;================= C Externs ==================
;==============================================
;; struct archive;
lostanza deftype LIBARCHIVE :
  la-ptr: ptr<?>

;; struct archive_entry;
lostanza deftype LIBARCHIVE_ENTRY :
  lae-ptr: ptr<?>

;; const char *archive_error_string(struct archive *);
extern archive_error_string : (ptr<LIBARCHIVE>) -> ptr<byte>

;; int archive_read_close(struct archive *);
extern archive_read_close : (ptr<LIBARCHIVE>) -> int

;; int archive_read_data_block(struct archive *a, const void **buff, size_t *size, la_int64_t *offset);
extern archive_read_data_block : (ptr<LIBARCHIVE>, ptr<ptr<?>>, ptr<long>, ptr<long>) -> int

;; int archive_read_free(struct archive *);
extern archive_read_free : (ptr<LIBARCHIVE>) -> int

;; struct archive *archive_read_new(void);
extern archive_read_new : () -> ptr<LIBARCHIVE>

;; int archive_read_next_header(struct archive *, struct archive_entry **);
extern archive_read_next_header : (ptr<LIBARCHIVE>, ptr<ptr<LIBARCHIVE_ENTRY>>) -> int

;; int archive_read_open_filename(struct archive *, const char *_filename, size_t _block_size);
extern archive_read_open_filename : (ptr<LIBARCHIVE>, ptr<byte>, long) -> int

;; int archive_read_support_compression_gzip(struct archive *);
extern archive_read_support_compression_gzip : (ptr<LIBARCHIVE>) -> int

;; int archive_read_support_format_tar(struct archive *);
extern archive_read_support_format_tar : (ptr<LIBARCHIVE>) -> int

;; int archive_write_close(struct archive *);
extern archive_write_close : (ptr<LIBARCHIVE>) -> int

;; la_ssize_t archive_write_data_block(struct archive *, const void *, size_t, la_int64_t);
extern archive_write_data_block : (ptr<LIBARCHIVE>, ptr<?>, long, long) -> long

;; struct archive *archive_write_disk_new(void);
extern archive_write_disk_new : () -> ptr<LIBARCHIVE>

;; int archive_write_disk_set_options(struct archive *, int flags);
extern archive_write_disk_set_options : (ptr<LIBARCHIVE>, long) -> int

;; int archive_write_finish_entry(struct archive *);
extern archive_write_finish_entry : (ptr<LIBARCHIVE>) -> int

;; int archive_write_free(struct archive *);
extern archive_write_free : (ptr<LIBARCHIVE>) -> int

;; int archive_write_header(struct archive *, struct archive_entry *);
extern archive_write_header : (ptr<LIBARCHIVE>, ptr<LIBARCHIVE_ENTRY>) -> int

;; const char *archive_entry_pathname(struct archive_entry *);
extern archive_entry_pathname : (ptr<LIBARCHIVE_ENTRY>) -> ptr<byte>

;; const char *archive_entry_hardlink(struct archive_entry *)
extern archive_entry_hardlink : (ptr<LIBARCHIVE_ENTRY>) -> ptr<byte>

;; void  archive_entry_set_pathname(struct archive_entry *, const char *);
extern archive_entry_set_pathname : (ptr<LIBARCHIVE_ENTRY>, ptr<byte>) -> int

;; void  archive_entry_set_hardlink(struct archive_entry *, const char *);
extern archive_entry_set_hardlink : (ptr<LIBARCHIVE_ENTRY>, ptr<byte>) -> int

;Constants
#for (V in [ARCHIVE_VERSION_NUMBER
            ARCHIVE_EOF
            ARCHIVE_OK
            ARCHIVE_RETRY
            ARCHIVE_WARN
            ARCHIVE_FAILED
            ARCHIVE_FATAL
            ARCHIVE_EXTRACT_OWNER
            ARCHIVE_EXTRACT_PERM
            ARCHIVE_EXTRACT_TIME
            ARCHIVE_EXTRACT_NO_OVERWRITE
            ARCHIVE_EXTRACT_UNLINK
            ARCHIVE_EXTRACT_ACL
            ARCHIVE_EXTRACT_FFLAGS
            ARCHIVE_EXTRACT_XATTR
            ARCHIVE_EXTRACT_SECURE_SYMLINKS
            ARCHIVE_EXTRACT_SECURE_NODOTDOT
            ARCHIVE_EXTRACT_NO_AUTODIR
            ARCHIVE_EXTRACT_NO_OVERWRITE_NEWER
            ARCHIVE_EXTRACT_SPARSE
            ARCHIVE_EXTRACT_MAC_METADATA
            ARCHIVE_EXTRACT_NO_HFS_COMPRESSION
            ARCHIVE_EXTRACT_HFS_COMPRESSION_FORCED
            ARCHIVE_EXTRACT_SECURE_NOABSOLUTEPATHS
            ARCHIVE_EXTRACT_CLEAR_NOCHANGE_FFLAGS
            ARCHIVE_EXTRACT_SAFE_WRITES]
      get_V in [get_ARCHIVE_VERSION_NUMBER
            get_ARCHIVE_EOF
            get_ARCHIVE_OK
            get_ARCHIVE_RETRY
            get_ARCHIVE_WARN
            get_ARCHIVE_FAILED
            get_ARCHIVE_FATAL
            get_ARCHIVE_EXTRACT_OWNER
            get_ARCHIVE_EXTRACT_PERM
            get_ARCHIVE_EXTRACT_TIME
            get_ARCHIVE_EXTRACT_NO_OVERWRITE
            get_ARCHIVE_EXTRACT_UNLINK
            get_ARCHIVE_EXTRACT_ACL
            get_ARCHIVE_EXTRACT_FFLAGS
            get_ARCHIVE_EXTRACT_XATTR
            get_ARCHIVE_EXTRACT_SECURE_SYMLINKS
            get_ARCHIVE_EXTRACT_SECURE_NODOTDOT
            get_ARCHIVE_EXTRACT_NO_AUTODIR
            get_ARCHIVE_EXTRACT_NO_OVERWRITE_NEWER
            get_ARCHIVE_EXTRACT_SPARSE
            get_ARCHIVE_EXTRACT_MAC_METADATA
            get_ARCHIVE_EXTRACT_NO_HFS_COMPRESSION
            get_ARCHIVE_EXTRACT_HFS_COMPRESSION_FORCED
            get_ARCHIVE_EXTRACT_SECURE_NOABSOLUTEPATHS
            get_ARCHIVE_EXTRACT_CLEAR_NOCHANGE_FFLAGS
            get_ARCHIVE_EXTRACT_SAFE_WRITES
            ]):
  extern get_V: () -> int
  public lostanza val V:ref<Int> = new Int{call-c get_V()}
#for (V in [ARCHIVE_VERSION_ONLY_STRING
            ARCHIVE_VERSION_STRING]
      get_V in [get_ARCHIVE_VERSION_ONLY_STRING
            get_ARCHIVE_VERSION_STRING
            ]):
  extern get_V: () -> ptr<byte>
  public lostanza val V:ref<String> = String(call-c get_V())


;============================================================
;======================= Wrappers ===========================
;============================================================
public lostanza defn libarchive_error_string (p:ref<LIBARCHIVE>) -> ref<String|False> :
  val pb:ptr<byte> = call-c archive_error_string(p.la-ptr)
  if pb == null :
    return false
  else :
    return String(pb)

public lostanza defn libarchive_read_close (p:ref<LIBARCHIVE>) -> ref<Int> :
  return new Int{call-c archive_read_close(p.la-ptr)}

public lostanza defn libarchive_read_data_block (p:ref<LIBARCHIVE>, pbuf:ptr<ptr<?>>, psize:ptr<long>, poffset:ptr<long>) -> ref<Int> :
  return new Int{call-c archive_read_data_block(p.la-ptr, pbuf, psize, poffset)}

public lostanza defn libarchive_read_free (p:ref<LIBARCHIVE>) -> ref<Int> :
  return new Int{call-c archive_read_free(p.la-ptr)}

public lostanza defn libarchive_read_new () -> ref<LIBARCHIVE> :
  return new LIBARCHIVE{call-c archive_read_new()}

public lostanza defn libarchive_read_next_header (p:ref<LIBARCHIVE>, q:ref<LIBARCHIVE_ENTRY>) -> ref<Int> :
  return new Int{call-c archive_read_next_header(p.la-ptr, addr!(q.lae-ptr))}

public lostanza defn libarchive_read_open_filename (p:ref<LIBARCHIVE>, f:ref<String>, bs:ref<Long>) -> ref<Int> :
  return new Int{call-c archive_read_open_filename(p.la-ptr, addr!(f.chars), bs.value)}

public lostanza defn libarchive_read_support_compression_gzip (p:ref<LIBARCHIVE>) -> ref<Int> :
  return new Int{call-c archive_read_support_compression_gzip(p.la-ptr)}

public lostanza defn libarchive_read_support_format_tar (p:ref<LIBARCHIVE>) -> ref<Int> :
  return new Int{call-c archive_read_support_format_tar(p.la-ptr)}

public lostanza defn libarchive_write_close (p:ref<LIBARCHIVE>) -> ref<Int> :
  return new Int{call-c archive_write_close(p.la-ptr)}

public lostanza defn libarchive_write_data_block (p:ref<LIBARCHIVE>, buf:ptr<?>, size:long, offset:long) -> ref<Long> :
  return new Long{call-c archive_write_data_block(p.la-ptr, buf, size, offset)}

public lostanza defn libarchive_write_disk_new () -> ref<LIBARCHIVE> :
  return new LIBARCHIVE{call-c archive_write_disk_new()}

public lostanza defn libarchive_write_disk_set_options (p:ref<LIBARCHIVE>, flags:ref<Int>) -> ref<Int> :
  return new Int{call-c archive_write_disk_set_options(p.la-ptr, flags.value)}

public lostanza defn libarchive_write_finish_entry (p:ref<LIBARCHIVE>) -> ref<Int> :
  return new Int{call-c archive_write_finish_entry(p.la-ptr)}

public lostanza defn libarchive_write_free (p:ref<LIBARCHIVE>) -> ref<Int> :
  return new Int{call-c archive_write_free(p.la-ptr)}

public lostanza defn libarchive_write_header (p:ref<LIBARCHIVE>, q:ref<LIBARCHIVE_ENTRY>) -> ref<Int> :
  return new Int{call-c archive_write_header(p.la-ptr, q.lae-ptr)}

public lostanza defn libarchive_entry_new () -> ref<LIBARCHIVE_ENTRY> :
  return new LIBARCHIVE_ENTRY{null}

public lostanza defn libarchive_entry_pathname (e:ref<LIBARCHIVE_ENTRY>) -> ref<String|False> :
  val pb:ptr<byte> = call-c archive_entry_pathname(e.lae-ptr)
  if pb == null :
    return false
  else :
    return String(pb)

public lostanza defn libarchive_entry_hardlink (e:ref<LIBARCHIVE_ENTRY>) -> ref<String|False> :
  val pb:ptr<byte> = call-c archive_entry_hardlink(e.lae-ptr)
  if pb == null :
    return false
  else :
    return String(pb)

;;extern archive_entry_set_pathname : (ptr<LIBARCHIVE_ENTRY>, ptr<byte>) -> int
public lostanza defn libarchive_entry_set_pathname (e:ref<LIBARCHIVE_ENTRY>, s:ref<String>) -> ref<False> :
  call-c archive_entry_set_pathname(e.lae-ptr, addr!(s.chars))
  return false

;;extern archive_entry_set_hardlink : (ptr<LIBARCHIVE_ENTRY>, ptr<byte>) -> int
public lostanza defn libarchive_entry_set_hardlink (e:ref<LIBARCHIVE_ENTRY>, s:ref<String>) -> ref<False> :
  call-c archive_entry_set_hardlink(e.lae-ptr, addr!(s.chars))
  return false


;============================================================
;================ High Level Functions ======================
;============================================================
defn errstr (i:Int) -> String :
  switch {i == _} :
    ARCHIVE_EOF    : "ARCHIVE_EOF"
    ARCHIVE_OK     : "ARCHIVE_OK"
    ARCHIVE_RETRY  : "ARCHIVE_RETRY"
    ARCHIVE_WARN   : "ARCHIVE_WARN"
    ARCHIVE_FAILED : "ARCHIVE_FAILED"
    ARCHIVE_FATAL  : "ARCHIVE_FATAL"
    else : to-string(i)


defn prepend-directory (dir:String, e:LIBARCHIVE_ENTRY) -> False :
  if dir != "" and dir != "." :
    match(libarchive_entry_pathname(e)) :
      (x:String) :
        var sep:String = "/"
        if x[0] == '/':
          sep = ""
        libarchive_entry_set_pathname(e, to-string("%_%_%_" % [dir, sep, x]))
      (_) : false

    match(libarchive_entry_hardlink(e)) :
      (x:String) :
        var sep:String = "/"
        if x[0] == '/':
          sep = ""
        libarchive_entry_set_hardlink(e, to-string("%_%_%_" % [dir, sep, x]))
      (_) : false
  false

public defn untar (filename:String, targetdir:String, -- verbose:True|False = false, dryrun:True|False = false) -> Int :
  val a = libarchive_read_new()
  val out = libarchive_write_disk_new()
  try:
    if verbose:
      println(STANDARD-ERROR-STREAM, "untar: ARCHIVE_VERSION_STRING = %_" % [ARCHIVE_VERSION_STRING])
    var r = ARCHIVE_OK

    val flags = ARCHIVE_EXTRACT_TIME | ARCHIVE_EXTRACT_PERM | ARCHIVE_EXTRACT_ACL | ARCHIVE_EXTRACT_FFLAGS
    r = libarchive_write_disk_set_options(out, flags);
    if r != ARCHIVE_OK :
      throw(Exception("libarchive_write_disk_set_options returned %_: %_" % [errstr(r), libarchive_error_string(a)]))

    r = libarchive_read_support_compression_gzip(a)
    if r != ARCHIVE_OK :
      throw(Exception("libarchive_read_support_compression_gzip returned %_: %_" % [errstr(r), libarchive_error_string(a)]))

    r = libarchive_read_support_format_tar(a)
    if r != ARCHIVE_OK :
      throw(Exception("libarchive_read_support_format_tar returned %_: %_" % [errstr(r), libarchive_error_string(a)]))

    r = libarchive_read_open_filename(a, filename, 10240L)
    if r != ARCHIVE_OK :
      throw(Exception("libarchive_read_open_filename returned %_: %_" % [errstr(r), libarchive_error_string(a)]))

    label<False> break :
      var entry = libarchive_entry_new()
      while true :
        r = libarchive_read_next_header(a, entry);
        if r == ARCHIVE_EOF :
          break(false)
        if r != ARCHIVE_OK :
          throw(Exception("libarchive_read_next_header returned %_: %_" % [errstr(r), libarchive_error_string(a)]))
        if verbose and not dryrun :
          print(STANDARD-ERROR-STREAM, "x ");
        if verbose and dryrun :
          print(STANDARD-ERROR-STREAM, "%_" % [libarchive_entry_pathname(entry)])
        if not dryrun :
          ;; update the entry filename by prepending targetdir
          prepend-directory(targetdir, entry)
          r = libarchive_write_header(out, entry)
          if r != ARCHIVE_OK :
            throw(Exception("libarchive_write_header returned %_: %_" % [errstr(r), libarchive_error_string(out)]))
          else :
            r = copy_data(a, out)
            if r != ARCHIVE_OK :
              throw(Exception("copy_data returned %_: %_" % [errstr(r), libarchive_error_string(out)]))
            r = libarchive_write_finish_entry(out)
            if r != ARCHIVE_OK :
              throw(Exception("libarchive_write_finish_entry returned %_: %_" % [errstr(r), libarchive_error_string(out)]))
        if verbose and dryrun :
          print(STANDARD-ERROR-STREAM, "\n")

    ARCHIVE_OK

  finally:
    libarchive_read_close(a)
    libarchive_read_free(a)

    libarchive_write_close(out)
    libarchive_write_free(out)


lostanza defn copy_data (ar:ref<LIBARCHIVE>, aw:ref<LIBARCHIVE>) -> ref<Int> :
  var r:ref<Int> = new Int{0}
  val pbuf:ptr<ptr<?>> = call-c clib/malloc(sizeof(ptr<?>))
  val psize:ptr<long> = call-c clib/malloc(sizeof(long))
  val poffset:ptr<long> = call-c clib/malloc(sizeof(long))
  ;val current-err:ptr<?> = call-c clib/get_stderr()

  ;call-c clib/fprintf(current-err, "untar: copy_data: begin\n")
  var finished:int = 0
  while finished == 0 :
    r = libarchive_read_data_block(ar, pbuf, psize, poffset)
    ;call-c clib/fprintf(current-err, "untar: copy_data: got result %d buf %p size %d offset %d\n", r.value, pbuf, [psize], [poffset])
    ;call-c clib/fflush(current-err)
    if r.value == ARCHIVE_EOF.value :
      r = ARCHIVE_OK
      finished = 1
    else :
      if r.value != ARCHIVE_OK.value :
        finished = 1
      else :
        val rr:ref<Long> = libarchive_write_data_block(aw, [pbuf], [psize], [poffset]);
        if (rr.value != ARCHIVE_OK.value) :
          finished = 1
  call-c clib/free(pbuf)
  call-c clib/free(psize)
  call-c clib/free(poffset)
  return r
